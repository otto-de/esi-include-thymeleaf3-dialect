plugins {
    id('java')
    id('eclipse')
    id('idea')
    id('maven-publish')
    id('signing')
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

version = '2.1.1-SNAPSHOT'
group = 'de.otto'

sourceCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

ext.libraries = [
        thymeleaf3: [
                'org.thymeleaf:thymeleaf',
                'org.thymeleaf:thymeleaf-spring6'
        ]
]

dependencies {
    ext.springBootVersion = '3.2.2'
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    compileOnly(libraries.thymeleaf3)

    implementation('org.springframework:spring-context')
    implementation('org.springframework:spring-beans')
    implementation('org.springframework.boot:spring-boot-autoconfigure')
    implementation('jakarta.servlet:jakarta.servlet-api')
    implementation('jakarta.annotation:jakarta.annotation-api')

    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation(libraries.thymeleaf3)
    testImplementation("org.mockito:mockito-core")
    testImplementation("org.hamcrest:hamcrest-core")
    testImplementation("org.hamcrest:hamcrest-library")
    testImplementation("org.awaitility:awaitility")
    testImplementation('org.springframework:spring-test')

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
}

test {
    useJUnitPlatform()
}

jar {
    manifest.attributes provider: 'gradle'
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'esi-include-thymeleaf3-dialect'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'esi-include-thymeleaf3-dialect'
                packaging 'jar'
                description = 'This library provides a thymeleaf3 dialect that resolves esi:include tags.Ã¼'
                url = 'http://github.com/otto-de/esi-include-thymeleaf3-dialect'

                scm {
                    url = 'scm:git@github.com:otto-de/esi-include-thymeleaf3-dialect.git'
                    connection = 'scm:git@github.com:otto-de/esi-include-thymeleaf3-dialect.git'
                    developerConnection = 'scm:git@github.com:otto-de/esi-include-thymeleaf3-dialect.git'
                }

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                    developer {
                        id = 'fbregulla'
                        name = 'Frank Bregulla'
                    }
                }
            }
        }
    }

}

java {
    withJavadocJar()
    withSourcesJar()
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}


